apply plugin: 'info.solidsoft.pitest.aggregator'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://repo.spring.io/snapshot" }
        maven { url = "https://repo.spring.io/milestone" }
        /*maven {
            url = "${azuredo_url}"
            credentials {
                username = "${azuredo_user}"
                password = "${azuredo_pass}"
            }
        }*/
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'org.sonarqube'

    // compileJava.dependsOn validateStructure // Commented out - validateStructure task not available

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    //build.dependsOn 'pitest'

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation libs.reactor.core
        implementation libs.reactor.extra

        // [Libraries]
        //implementation libs.example.logging
        //implementation libs.example.core

        implementation libs.slf4j.api
        testImplementation libs.blockhound.junit

        // [AWS SDK v2]
        implementation libs.awssdk.sts

        testImplementation libs.reactor.test
        testRuntimeOnly libs.platform.launcher
        testImplementation libs.boot.test
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor  "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly  "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor  "org.projectlombok:lombok:${lombokVersion}"
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        testImplementation("org.wiremock:wiremock:3.5.4")

        constraints {
            implementation("commons-io:commons-io:2.14.0")
            implementation("org.apache.commons:commons-lang3:3.19.0")
            implementation("com.google.guava:guava:32.1.2-jre")
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "io.netty:netty-bom:4.1.130.Final"
            dependency 'org.apache.commons:commons-lang3:3.19.0'
        }
    }

    configurations.configureEach {

        exclude group: 'commons-logging', module: 'commons-logging'

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->

            if (details.requested.group == "io.netty") {
                details.useVersion("4.1.130.Final")
                details.because("Fix CVE-2025-67735 – enforced Netty LTS")
            }

            if (details.requested.group == "commons-fileupload") {
                details.useVersion("1.6.0")
            }
        }
    }

    dependencyCheck {
        formats = ['HTML', 'JSON', 'XML']
        failBuildOnCVSS = 11

        scanConfigurations = [
                'runtimeClasspath',
                'compileClasspath'
        ]

        nvd {
            apiKey = System.getenv('NVD_API_KEY') ?: project.findProperty('nvdApiKey')
        }

        analyzers {
            assemblyEnabled = false
            nuspecEnabled = false
            jarEnabled = true
        }
    }

    tasks.withType(Test).configureEach {
        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)) {
            jvmArgs += [
                    "-XX:+AllowRedefinitionToAddDeleteMethods"
            ]
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    pitest {
        targetClasses = ['com.example.*']
        excludedClasses = []
        excludedTestClasses = []
        pitestVersion = '1.20.6'
        verbose = false
        outputFormats = ['XML', 'HTML']
        threads = 8
        exportLineCoverage = true
        useClasspathFile = true
        timestampedReports = false
        //mutators = ['STRONGER', 'DEFAULTS']
        fileExtensionsToFilter.addAll('xml', 'orbit')
        junit5PluginVersion = '1.2.3'
        failWhenNoMutations = false
        jvmArgs = [
            "-XX:+EnableDynamicAgentLoading",
            "-XX:+AllowRedefinitionToAddDeleteMethods"
        ]
    }

    sonarqube {
        properties {
            property "sonar.host.url", System.getenv("SONAR_HOST_URL")
            property "sonar.token", System.getenv("SONAR_TOKEN")
            property "sonar.organization", System.getenv("SONAR_ORGANIZATION")
            property "sonar.projectKey", System.getenv("SONAR_PROJECT_KEY")
            property "sonar.projectName", "SMS Notification Management"
            property "sonar.projectVersion", "1.0.0"

            // Exclusiones de cobertura para que coincidan con JaCoCo
            property "sonar.coverage.exclusions", [
                    "**/config/**",
                    "**/configuration/**",
                    "**/MainApplication.java",
                    '**/*Config.class',
                    "**/models/**",
                    "**/dto/**",
                    "**/mapper/**",
                    "**/mappers/**",
                    '**/*MapperImpl.class',
                    "**/entity/**",
                    "**/exception/consts/**",
                    "**/*\$Builder.java",
                    "**/Test.java",
                    "**/Tests.java",
                    "**/TestCase.java"
            ].join(",")

            // SonarQube buscará automáticamente en build/reports/jacoco/test/jacocoTestReport.xml

            // Integración con OWASP Dependency-Check - solo para el módulo boot
            if (project.name == 'boot') {
                property "sonar.dependencyCheck.reportPath", "build/reports/dependency-check-report.xml"
                property "sonar.dependencyCheck.htmlReportPath", "build/reports/dependency-check-report.html"
                property "sonar.dependencyCheck.summarize", "true"
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.required = true
            html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
            xml.required = true
            xml.outputLocation = layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml')
            csv.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/config/**',
                        '**/configuration/**',
                        '**/MainApplication.class',
                        '**/*Config.class',
                        '**/models/**',
                        '**/dto/**',
                        '**/mapper/**',
                        '**/*$Builder.class',
                        '**/*MapperImpl.class',
                        '**/Test.class',
                        '**/Tests.class',
                        '**/TestCase.class'
                ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport

        // Solo aplicar verificación de cobertura a módulos principales
        onlyIf {
            (project.name in ['app-service', 'domain'] ||
                    project.path.contains(':infrastructure:driven-adapters:external-api') ||
                    project.path.contains(':infrastructure:entry-points:reactive-web')) &&
                    file("${project.projectDir}/src/test/java").exists()
        }

        violationRules {
            rule {
                enabled = true
                element = 'BUNDLE'
                includes = [
                        'com.example.*.handler.*',
                        'com.example.*.usecases.*',
                        'com.example.*.adapter.*'
                ]
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.70
                }
            }
            rule {
                enabled = true
                element = 'BUNDLE'
                includes = [
                        'com.example.*.handler.*',
                        'com.example.*.usecases.*',
                        'com.example.*.adapter.*'
                ]
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.70
                }
            }
        }
    }

    check.dependsOn jacocoTestCoverageVerification

}

jacoco {
    toolVersion = "${jacocoPluginVersion}"
    reportsDirectory.set(layout.buildDirectory.dir("reports"))
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn = [subprojects.test, subprojects.jacocoTestReport]
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.setRequired true
        csv.setRequired false
        html.setRequired true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

pitestReportAggregate {
    doLast {
        def reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
        def consolidatedReport = new File(reportDir, 'mutations.xml')
        consolidatedReport.withWriter { writer ->
            writer.write("<mutations>\n")
            subprojects.each { subproject ->
                def xmlReport = subproject.layout.buildDirectory.file("reports/pitest/mutations.xml").get().asFile
                if (xmlReport.exists()) {
                    def xmlContent = xmlReport.text
                    xmlContent = xmlContent.replaceAll("<\\?xml[^>]*>", "")
                    xmlContent = xmlContent.replaceAll("</?mutations( partial=\"true\")?>", "")
                    writer.write(xmlContent.trim() + "\n")
                }
            }
            writer.write("</mutations>")
        }
    }
}

tasks.named('wrapper') {
    gradleVersion = '8.14.3'
}